plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
}

// https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("ignore_folder/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile rootProject.file("ignore_folder/release-keystore.jks")
            keyAlias "mbjc"
            storePassword keystoreProperties["storePassword"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
    compileSdk 32

    defaultConfig {
        applicationId "ly.david.mbjc"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        debug {
//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    namespace 'ly.david.mbjc'
}

dependencies {

    implementation "androidx.core:core-ktx:1.8.0"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    implementation "androidx.compose.material3:material3:1.0.0-alpha09"
    implementation "androidx.activity:activity-compose:1.5.0"
    implementation "androidx.navigation:navigation-compose:2.5.0"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.0"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"

    // https://developer.android.com/jetpack/androidx/releases/paging#1.0.0-alpha15
    // LazyPagingItems now sets the initial loadState to have a LoadState.Loading refresh.
    // https://issuetracker.google.com/issues/224855902
    implementation "androidx.paging:paging-compose:1.0.0-alpha14"

    // Room
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"
    implementation "androidx.room:room-ktx:2.4.2"
    implementation "androidx.room:room-paging:2.4.2"
    androidTestImplementation "androidx.room:room-testing:2.4.2"

    // Hilt
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-compiler:2.42"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.12.4"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}
