import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext {
        compose_version = "1.3.3"
        compose_material_version = "1.3.1"
        hilt_version = "2.45"
        moshi_version = "1.14.0"
        retrofit_version = "2.9.0"
        room_version = "2.5.0"
    }
    dependencies {
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.22.0"
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id "com.android.application" version '7.4.1' apply false
    id "com.android.library" version '7.4.1' apply false
    id "org.jetbrains.kotlin.android" version "1.8.0" apply false
    id "io.gitlab.arturbosch.detekt" version "1.22.0" apply false
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    repositories {
        google()
        mavenCentral()
    }

    // ./gradlew assembleRelease -Pmbjc.enableComposeCompilerReports=true --rerun-tasks
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]

            if (project.findProperty("mbjc.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }

    detekt {
        buildUponDefaultConfig = true
        allRules = false
        parallel = true

        // Use a single config file
        config = files("${project.rootDir}/config/detekt.yml")

        // Each module has its own baseline otherwise they overwrite each other
        baseline = file("${projectDir}/config/baseline.xml")
    }
    tasks.withType(Detekt).configureEach {
        reports {
            // observe findings in your browser with structure and code snippets
            html.required.set(true)

            // similar to the console output, contains issue signature to manually edit baseline files
            txt.required.set(true)
        }
    }

    // TODO: only need to apply this to any ui modules
    dependencies {
        detektPlugins "com.twitter.compose.rules:detekt:0.0.26"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
